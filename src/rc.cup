
import java_cup.runtime.*;
import java.util.Vector;

//--------------------------------------------------------------------
//
//--------------------------------------------------------------------

parser code 
{:
:};

scan with {: return new Symbol (sym.EOF); :};

//--------------------------------------------------------------------
//
//--------------------------------------------------------------------

terminal
    T_AMPERSAND,
    T_AND,
    T_ARROW,
    T_ASSIGN,
    T_BAR,
    T_BOOL,
    T_BREAK,
    T_CARET,
    T_CHAR,
    T_CIN,
    T_COLON,
    T_COLONCOLON,
    T_COMMA,
    T_CONST,
    T_CONTINUE,
    T_COUT,
    T_DELETE,
    T_DECLTYPE,
    T_DOT,
    T_ELSE,
    T_EQU,
    T_EXIT,
    T_EXTERN,
    T_FALSE,
    T_FLOAT,
    T_FUNCPTR,
    T_FUNCTION,
    T_GT,
    T_GTE,
    T_IF,
    T_INT,
    T_ISTREAM,
    T_LBRACE,
    T_LBRACKET,
    T_LPAREN,
    T_LT,
    T_LTE,
    T_MINUS,
    T_MINUSMINUS,
    T_MOD,
    T_NEQ,
    T_NEW,
    T_NOT,
    T_NULLPTR,
    T_OR,
    T_OSTREAM,
    T_PLUS,
    T_PLUSPLUS,
    T_RBRACE,
    T_RBRACKET,
    T_RETURN,
    T_RPAREN,
    T_SEMI,
    T_SIZEOF,
    T_SLASH,
    T_STAR,
    T_STATIC,
    T_STRUCTDEF,
    T_TRUE,
    T_TYPEDEF,
    T_WHILE,
    T_VOID,
    T_AUTO
    ;

terminal String
    T_CHAR_LITERAL,
    T_FLOAT_LITERAL,
    T_INT_LITERAL,
    T_STR_LITERAL,
    T_ID_U,
    T_ID,
    T_ENDL
    ;


//--------------------------------------------------------------------
//
//--------------------------------------------------------------------

non terminal    
    Program,
    OptGlobalDecls,
    GlobalDecls,
    GlobalDecl,
    FuncDef,
    FuncDecl,
    ExternDecl,
    VarDecl,
    ConstDecl,
    TypedefDecl,

    OptStmtList,
    StmtList,
    Stmt,
    CodeBlock,
    IfStmt,

    WhileStmt,
    BreakStmt,
    ContinueStmt,
    ExitStmt,
    ReturnStmt,
    ReadStmt,
    WriteStmt,
    
    //Phase 0
    NewStmt,
    DeleteStmt,
    Equality,
    Relation,
    ArrElemsList
    ;

non terminal String    
    AddOp,
    MulOp,
    IncDecOp,
    OptRef,
    OptElse,
    OptStatic,
    UnarySign,
    WritePairList,
    WritePair
    ;


non terminal STO
    Expr,
    Expr0,
    Expr1,
    Expr2,
    Expr3,
    Expr4,
    Expr5,
    Expr6,
    Expr7,
    Expr8,
    Designator,
    Designator2,
    Designator3,
    QualIdent,
    OptInit,
    ParamDecl,
    ConstExpr,
    FieldsList
    ;

non terminal Type
    BasicType,
    SubType,
    Type,
    UndecoratedType,
    ReturnType,
    DeclareType,
    OptModifiers, 
    Pointer,   
    PointerList,
	OptPointerList
    ;
    	
non terminal Vector
    IdList,
    IdListUpper,
    IdListWOptInit,
    IdListWInit,
    OptParamList,
    ParamList,
    OptExprList,
    ExprList,
    FieldList
    ;

//--------------------------------------------------------------------
//
//--------------------------------------------------------------------

Program ::=    {:
                         ((MyParser) parser).DoProgramStart(); 
               :}
               OptGlobalDecls
               {:
                       ((MyParser) parser).DoProgramEnd();
               :}
    ;

OptGlobalDecls ::=  GlobalDecls
    |                     /* empty */
    ;

GlobalDecls ::= GlobalDecl
    |               GlobalDecls GlobalDecl
    ;


GlobalDecl ::=  ExternDecl
    |               VarDecl
    |               ConstDecl
    |               TypedefDecl
    |               FuncDecl
    |               FuncDef
    ;

ExternDecl ::= T_EXTERN UndecoratedType:_2 IdList:_3 T_SEMI
                    {:
                         ((MyParser) parser).DoExternDecl (_3, _2);
                    :}
    ;

VarDecl ::=     OptStatic:_1 UndecoratedType:_2 IdListWOptInit:_3 T_SEMI
                    {:
                         //If error occured, which return a empty vector
                         if(_3.size() == 0){
                         }
                         else{
	                         //If the static keyword is provided
	                         if(_1 != null){
	                           Vector<VarSTO> v = new Vector<VarSTO>();
	                           for(int i = 0; i < _3.size(); i++){
	                             VarSTO s = (VarSTO)_3.elementAt(i);
	                             s.setStatic(true);
	                             //s.setType(_2);
	                             v.addElement(s);
	                           }
	                           ((MyParser) parser).DoVarDecl (v,_2);
	                         }
	                         else{
	                            ((MyParser) parser).DoVarDecl (_3,_2);
	                         }
	                     }
                    :}
    |           OptStatic:_3 T_AUTO T_ID:_1 T_ASSIGN Expr:_2 T_SEMI
                {: 
                  // Assign _1 with the type _2 have and put in a symbol table
                  ((MyParser) parser).DoAutoDeclaration(_1,_2,_3);
                :}

    ;

ConstDecl ::=  OptStatic:_1 T_CONST UndecoratedType:_2 IdListWInit:_3 T_SEMI
                    {:
                       //If the static keyword is provided
                       if(_1 != null){
                         Vector<VarSTO> v = new Vector<VarSTO>();
                         for(int i = 0; i < _3.size(); i++){
                           VarSTO s = (VarSTO)_3.elementAt(i);
                           s.setStatic(true);
                           v.addElement(s);
                         }
                         ((MyParser) parser).DoConstDecl (v, _2);
                       }
                       else{
                         ((MyParser) parser).DoConstDecl (_3, _2);
                       }
                   
                    :}
    |          OptStatic:_3 T_CONST T_AUTO T_ID:_1 T_ASSIGN Expr:_2 T_SEMI
               {: 
                 // Assign _1 with the type _2 have and put in a symbol table
    			 //TODO, auto static
		 		  ((MyParser) parser).DoConstAutoDeclaration(_1,_2);
               :}
    ;


OptStatic ::=   T_STATIC
				{:
				  RESULT = "static";
				:}
    |               /* empty */
    ;


TypedefDecl ::=     T_TYPEDEF Type:_1 IdListUpper:_2 T_SEMI
                    {:
                        //insert TypeDef STO into the symbol table
                        ((MyParser) parser).DoTypedefDecl (_1, _2);
                    :}
    |               T_STRUCTDEF T_ID_U:_1 T_LBRACE
                    {:
                        //Put the struct name into the symbol table for recursive define
                        ((MyParser) parser).DoStructdefDecl(_1);
                        ((MyParser) parser).DoBlockOpen();
                       
                    :}
                    FieldsList:_2 T_RBRACE T_SEMI
                    {:
                       //If error occurs
                       if(_2 != null){
                         //Get the vector of locals from the current scope, which is the fields of the struct
                         ((MyParser) parser).DoBlockClose();
                       }else{
                         //Get the vector of locals from the current scope, which is the fields of the struct
                         ((MyParser) parser).DoStructFieldFill();
                         ((MyParser) parser).DoBlockClose();
                       }
                       ((MyParser) parser).DoStructdefDeclEnd();
                    :}
    ;


FieldsList ::=   FieldList:_1
				 {:
				   RESULT = ((MyParser) parser).DoStructFieldDecl(_1);
				 :}
    |            FieldsList:_1 FieldList:_2
    			 {:
    			  
    			   if(_1 != null && _1.isError()){
    			     //If error already occur, still keep checking but return error
    			     ((MyParser) parser).DoStructFieldDecl(_2);
    			     RESULT = _1;
    			   }
    			   else{
    			     RESULT = ((MyParser) parser).DoStructFieldDecl(_2);
    			   }
    			 :}
    ;

FieldList ::=     UndecoratedType:_1 IdList:_2 T_SEMI
				  {:
				     //Vector of 0 size, error
				     if(_2.size() == 0){
				       RESULT = _2;
				     }
				     Vector<VarSTO> v = new Vector<VarSTO>();
				     for(int i = 0; i < _2.size(); i++){
				       VarSTO tmp = (VarSTO)_2.elementAt(i);
				       //If the type is not set
				       
				       if(tmp.getType() == null){
				         tmp.setType(_1);
				       }
				       else if(tmp.getType().isError()){
				         RESULT = new Vector();
				       }
				       else if(tmp.getType().isStruct()){				         
				       }
				       else if(tmp.getType().isArray()){
				         ((ArrayType)tmp.getType()).setElementType(_1.clone());
				         tmp.getType().setSize(((ArrayType)tmp.getType()).getArraySize() 
							* (_1.getSize()));
					     //Set name
					     ((ArrayType)tmp.getType()).setName(_1.getName()+"[" + 
							((ArrayType)(tmp.getType())).getArraySize() + "]");
				       }
				       else if(tmp.getType().isPointer()){
				         ((PointerType)tmp.getType()).setElementType(_1.clone());
					     //Get name of the pointer
					     ((PointerType)tmp.getType()).setName(
							((PointerType)tmp.getType()).getPrintedName() + "*");
					   }
				       else{
				         ((CompositeType)(tmp.getType())).setElementType(_1);
				         //check if is an array type ,set the size
				         
				       }
				       v.addElement(tmp);
				     }
				     RESULT = v;
				  :}
    ;



FuncDef ::=    T_FUNCTION T_COLON ReturnType:_1 OptRef:_4 T_ID:_2
                    {:
                        ((MyParser) parser).SaveLineNum ();
						if(_4.equals("&"))
						  _1.setReference(true);
                        ((MyParser) parser).DoFuncDecl_1(_1, _2);
                    :}
                    T_LPAREN OptParamList:_3 T_RPAREN
                    {:
                        ((MyParser) parser).DoFormalParams(_3);
                    :}
                    T_LBRACE OptStmtList T_RBRACE
                    {:
                    	//Close scope 
                        ((MyParser) parser).DoFuncDecl_2();
                    :}
    ;


FuncDecl ::=   T_EXTERN T_FUNCTION T_COLON ReturnType:_1 T_ID:_2
                    {:
                        ((MyParser) parser).DoExternFuncDecl_1(_1, _2);
                    :}
                    T_LPAREN OptParamList:_3 T_RPAREN
                    {:
                        ((MyParser) parser).DoExternFormalParams(_3);
                    :}
                    T_SEMI
                    {:
                        ((MyParser) parser).DoExternFuncDecl_2();
                    :}
    ;


Type ::=         SubType:_1 OptModifiers:_2
				 {:
				   //If there's modifiers
				   if(_2 != null){
				     if(_2.isError()){
				       RESULT = _2;
				     }
				     else{
				       ((CompositeType)_2).setElementType(_1);
				       RESULT = _2;
				       if(_2.isArray()){
				         ((CompositeType)_2).setName(_1.getName()+"[" + 
							((ArrayType)(_2)).getArraySize() + "]");
					     int size = 0;
					     for(int j = 0; j < ((ArrayType)(_2)).getArraySize(); j++){
						   size += _1.getSize();
					     }
					     ((ArrayType)_2).setSize(size);
				       }
				       else if(_2.isPointer()){
					     //Get name of the pointer
					     ((PointerType)_2).setName(
							((PointerType)_2).getPrintedName() + "*");
					   }
				     }
				   }
				   else{
				     RESULT = _1;
				   }
				 :}
    |    T_FUNCPTR T_COLON ReturnType:_1 OptRef:_2 T_LPAREN OptParamList:_3 T_RPAREN
    	 {:
    	    //Store the information of the function pointer type and return
    	    FunctionPointerType type = new FunctionPointerType("funcptr", 4);
    	    //If return by reference
    	    if(_2.equals("&"))
			  _1.setReference(true);
		    type.setReturnType(_1);
		    type.setParameters(_3);
		    type.setName(type.getErrorName());
		    RESULT = type;
    	 :}
    ;

UndecoratedType ::= SubType:_1
					{:
					  RESULT = _1; 
					:}
    |               T_FUNCPTR T_COLON ReturnType:_1 OptRef:_2 T_LPAREN OptParamList:_3 T_RPAREN
    				{:
    				  //Check#18b
    				  //Store the information of the function pointer type and return
    	    		  FunctionPointerType type = new FunctionPointerType("funcptr", 4);
			    	  //If return by reference
			    	  if(_2.equals("&"))
						_1.setReference(true);
					  type.setReturnType(_1);
					  type.setParameters(_3);
					  type.setName(type.getErrorName());
					  RESULT = type;
    				:}
    ;

SubType ::=    QualIdent:_1
			   {:
			     //Get the basic type out of TypedefSTO
			     //modifiy the previous type and set its name to be the correct typedef ID
			     if(_1.isError()){
			       RESULT = new ErrorType("type not found", 4);
			     }
			     else{
			       Type t = _1.getType().clone();
				   t.setName(_1.getName());
			       RESULT = t;
			     }
			   :}
			   
    |               BasicType:_1
    				{:
    				  RESULT = _1;
    				:}
    |			DeclareType:_1
    		    {:
    			  //Add for check#3c decltype keyword
    			  RESULT = _1;
    			:}
    ;

DeclareType ::=  T_DECLTYPE
				 {:
				   ((MyParser) parser).doDecltype();
				 :}
				 T_LPAREN Expr:_1 T_RPAREN
				 {:
				   ((MyParser)parser).doDecltypeDone();
				    if(_1.isError())
				      RESULT = new ErrorType("decl error", 4);
				    else{
				      Type t = _1.getType().clone();	
				      t.setName(_1.getType().getName());
				      RESULT = t;
				    }
				     
				 :}
	;

BasicType ::=       T_INT 
					{:
					  RESULT = new IntType("int", 4);
					:}
    |               T_FLOAT
    				{:
    				  RESULT = new FloatType("float", 4);
    				:}
    |               T_BOOL
   					{:
    				  RESULT = new BoolType("bool", 4);
    				:}
    |               T_CHAR
    ;


OptModifiers ::= 	PointerList:_1
					{:
					  RESULT = _1;
					:}
	|				T_LBRACKET ConstExpr:_1 T_RBRACKET
					{:
					  //Modifier for array type
					  //Check #10, illegal array declaration
					  RESULT = ((MyParser) parser).DoArrayDeclCheck(_1);
					:}
    |               /* empty */
    ;
	
OptPointerList ::= 	PointerList: _1
					{:
					  RESULT = _1;
					:}
    |               /* empty */
    ;


PointerList ::=    Pointer:_1
				   {:
				     RESULT = _1;
				   :}
    |              PointerList:_1 Pointer:_2
    			   {:
    			     ((PointerType)_2).setElementType(_1);
    			     RESULT = _2;
    			   :}
    ;


Pointer ::=        T_STAR:_1
				   {:
				     RESULT = new PointerType("pointer", 4);
				   :}
    ;


ReturnType ::=      SubType:_1 OptPointerList:_2
                    {:
                        // You should determine the type and return it here
                        if(_2 != null){
                          ((PointerType)_2).setElementType(_1);
                          _2.setName(((PointerType)_2).getPrintedName()+"*");
                          RESULT = _2;
                        }else{
                          RESULT = _1;
                        }
                    :}
    |               T_VOID
    				{:
    				  RESULT = new VoidType("void", 4);
    				:}

    ;


CodeBlock ::=       T_LBRACE
                    {: ((MyParser) parser).DoBlockOpen(); :}
                    OptStmtList
		            {: 
		              ((MyParser) parser).DoBlockClose();     
		            :}
                    T_RBRACE
    ;


OptStmtList ::=     StmtList
    |               /* empty */
    ;


StmtList ::=        Stmt
    |               StmtList Stmt
    ;


Stmt ::=        VarDecl
    |               ConstDecl
    |               TypedefDecl
    |               CodeBlock
    |               Expr T_SEMI
    |               IfStmt
    |               WhileStmt
    |               BreakStmt
    |               ContinueStmt
    |               ExitStmt
    |               ReturnStmt
    |               ReadStmt
    |               WriteStmt
    |				NewStmt
    |				DeleteStmt
    ;


OptParamList ::=    ParamList:_1
					{:
					  //Return a list of parameter
					  RESULT = _1;
					:}
    |               /* empty */
    				{:
    				  RESULT = new Vector<STO>();
    				:}
    ;


ParamList ::=       ParamDecl:_1
					{:
					  //Return the vector of parameter types
					  Vector<STO> v = new Vector<STO>();
					  v.addElement(_1);
					  RESULT = v;
					:}
    |               ParamList:_1 T_COMMA ParamDecl:_2
    				{:
    				  //Return the vector with the _2 add to the back
    				  _1.addElement(_2);
    				  RESULT = _1;
    				:}
    ;


ParamDecl ::=       Type:_1 OptRef:_3 T_ID:_2
					{:
					  //Check #5 parameter type check, return the type's name
					  //if OptRef is a reference
					  if(_3.equals("&")){
					    _1.setReference(true);
					    RESULT = new VarSTO(_2, _1);
					  }
					  else
					    RESULT = new VarSTO(_2, _1);
					  
					:}
    ;


OptRef ::=          T_AMPERSAND

					{: //Here is parameter pass by reference
					   RESULT = "&";
					 :}
    |               /* empty */	
    				 {: //Here is parameter pass by value
    				 	RESULT ="";		
					 :}	
    ;


IdList ::=          OptModifiers:_2 T_ID:_1
                    {:
                        VarSTO v = new VarSTO(_1);
                        if(_2 != null){
                          if(_2.isError()){
                            //Return an empty vector to indicate an error
                            RESULT = new Vector();
                          }
                          else{
                            //Composite type
                            v.setType(_2);
                            RESULT = new Vector();
                            RESULT.addElement(v);
                          }
                        }
                        //No optional modifiers
                        else{
                          //Return an vector of VarSTO
                          RESULT = new Vector ();
                          RESULT.addElement (v);
                        }
                    :}
    |               IdList:_1 T_COMMA OptModifiers:_2 T_ID:_3
                    {:
                        RESULT = _1;
                        VarSTO v = new VarSTO(_3);
                        if(_2 != null){
                          if(_2.isError()){
                            //Return an empty vector to indicate an error
                            RESULT = new Vector();
                          }
                          else{
                            v.setType(_2);
                            RESULT.addElement(v);
                          }
                        }
                        //No optional modifiers
                        else{
                          //Return an vector of VarSTO
                          RESULT.addElement (v);
                        }
                    :}
    ;


IdListUpper ::=     T_ID_U:_1
                    {:
                        RESULT = new Vector ();
                        RESULT.addElement (_1);
                    :}
    |               IdListUpper:_1 T_COMMA T_ID_U:_3
                    {:
                        RESULT = _1;
                        RESULT.addElement (_3);
                    :}
    ;

IdListWOptInit ::=  OptModifiers:_3 T_ID:_1 OptInit:_2
                    {:
                        if(_2 != null)
                        {
                          if(_2.isError())
                            RESULT = new Vector();
                          else{
	                        //Check if the OptInit is known at compile time
	                        VarSTO v = new VarSTO(_1);
	                        v.setInit(_2);
	                        v.setStatic(false);
	                        if(_3 != null)
	                          v.setType(_3);	                      
	                        RESULT = new Vector();
	                        RESULT.addElement (v);
	                      }
	                    }
	                    //No init applied
	                    else{
	                      VarSTO v = new VarSTO(_1);
	                      v.setStatic(false);
	                      if(_3 != null)
	                        v.setType(_3);	                      
	                      RESULT = new Vector();
	                      RESULT.addElement (v);
	                    }
                    :}
    |               IdListWOptInit:_1 T_COMMA OptModifiers:_4 T_ID:_3 OptInit:_2
                    {:
                        if(_2 != null){
	                        if(_2.isError())
	                          RESULT = new Vector();
	                        else{
		                        RESULT = _1;
		                        VarSTO v = new VarSTO(_3);
		                        v.setInit(_2);
		                        v.setStatic(false);
		                        //If there's modifier
		                        if(_4 != null)
			                        v.setType(_4);
		                        RESULT.addElement (v);
		                    }
		                }
		                //No init applied
	                    else{
	                      RESULT = _1;
		                  VarSTO v = new VarSTO(_3);
		                  v.setStatic(false);
		                  //If there's modifier
		                  if(_4 != null)
			                v.setType(_4);
		                  RESULT.addElement (v);
	                    }
                    :}
    ;

IdListWInit ::=     OptModifiers T_ID:_1 T_ASSIGN ConstExpr:_2
                    {:
                      if(_2.isError()){
                        RESULT = new Vector();
                      }
                      else{
                        VarSTO v = new VarSTO(_1);
                        v.setInit(_2);
                        v.setStatic(false);
			            RESULT = new Vector ();
			            RESULT.addElement (v);
			          }
                    :}
  //|       Do not need an extra rule since we are not doing constant arrays
    |   IdListWInit:_1 T_COMMA OptModifiers T_ID:_3 T_ASSIGN ConstExpr:_2
        {:
		  RESULT = _1;
		  if(_2.isError()){
            RESULT = new Vector();
          }
          else{
            VarSTO v = new VarSTO(_3);
            v.setInit(_2);
            v.setStatic(false);
			RESULT.addElement (v);
		  }
	    :}
    ;


OptInit ::=         T_ASSIGN Expr:_1
					{:
					  RESULT = _1;
					:}
    |               T_ASSIGN T_LBRACE ArrElemsList T_RBRACE
    |               /* empty */
    ;

ArrElemsList ::=    Expr
    |               ArrElemsList T_COMMA Expr
    ;

IfStmt ::=          T_IF Expr:_1
					{:
					  RESULT = ((MyParser) parser).DoIfWhileExpr(_1);
					  ((MyParser)parser).DoIfYesStmt(_1);
					:}
					CodeBlock
					{:
					  ((MyParser)parser).DoIfYesStmtEnd();
					:} 
					OptElse:_2
					{:
					  if(_2 == null)
					    ((MyParser)parser).DoElseStmtStart();
					  ((MyParser)parser).DoIfStmtEnd();
					:}
    ;


OptElse ::=         T_ELSE
					{:
					  ((MyParser)parser).DoElseStmtStart();
					  RESULT = "else";
					:} 
					CodeBlock
    |               /* empty */
    ;


WhileStmt ::=       T_WHILE
					{:
					  //Set label at the beginning
					  ((MyParser)parser).DoWhileBegin();
					:}
				    Expr:_1
					{:
					  //Set in while to be true if it was false, increment the while level
					  ((MyParser)parser).DoWhileStmt(_1);
					:} 
					CodeBlock
					{:
					  RESULT = ((MyParser) parser).DoIfWhileExpr(_1);
					  ((MyParser) parser).DoWhileLoopCheck(_1);
					:}
    ;


BreakStmt ::=       T_BREAK:_1 T_SEMI
                    {: 
                      //Check illegal break statement
                      //break statement should be inside the while loop
                      ((MyParser) parser).DoBreakStmtCheck(); 
                    :}
    ;


ContinueStmt ::=    T_CONTINUE T_SEMI
                    {: 
                      //Check illegal continue statement
                      //continue statement should be inside the while loop
                      ((MyParser) parser).DoContinueStmtCheck(); 
                    :}
    ;


ExitStmt ::=        T_EXIT T_LPAREN Expr:_1 T_RPAREN T_SEMI
                    {:
                       //DoExitStmtCheck if Expr is assignable to an int
                       ((MyParser) parser).DoExitStmtCheck(_1); 
                    :}
    ;


ReturnStmt ::=   T_RETURN T_SEMI
  				 {:
  				   //((MyParser) parser).DoReturnCheck(new VoidType("void",4));
  				   ((MyParser) parser).DoReturnCheck(new ExprSTO("void",new VoidType("void",4)));
  				 :}
    |            T_RETURN Expr:_1 T_SEMI
         	     {:
         	       ((MyParser)parser).DoReturnCheck(_1); 
         	     :}
    ;


ReadStmt ::=    T_CIN T_ISTREAM Designator:_1 T_SEMI
				{:
				   ((MyParser) parser).DoCinStmt(_1);
				:}	
    ;

//Phase 0
NewStmt  ::= 	T_NEW Designator:_1 T_SEMI
                {: 
                   //Check the VarSTO is valid for new statement
                    ((MyParser) parser).DoNewStmtCheck(_1);      
                         
                :} 
	;

DeleteStmt ::=  T_DELETE Designator:_1 T_SEMI
                {: 
                    //Check the VarSTO is valid for delete statement
                    ((MyParser) parser).DoDeleteStmtCheck(_1);  
                :}
	;

WriteStmt ::=       
            T_COUT T_OSTREAM WritePairList:_1 T_SEMI
 		    {:
		      //((MyParser)parser).DoCoutStmt(_1);
		    :}
    ;

WritePairList ::=   WritePair:_1
		    {:
		      /*String tmp = "";
		      tmp += _1;
		      RESULT = tmp;*/
		    :}
    |       WritePairList:_1 T_OSTREAM WritePair:_2
    		{:
		      /*_1 += _2;
		      RESULT = _1;*/
		    :}
    ;


WritePair ::=    
       Expr:_1
	   {:
		 ((MyParser)parser).DoCoutStmt(_1);
		 //TODO, cout << a << endl
	   :}
    |  T_ENDL
       {:
          STO tmp = new ConstSTO("\n");
		  ((MyParser)parser).DoCoutStmt(tmp);
	    :}
    ;


ConstExpr ::=    Expr:_1
                 {:
                   RESULT = _1; 
                 :}
    ;


OptExprList ::=     ExprList:_1
					{:
					  RESULT = _1;
					:}
    |               /* empty */
    				{:
    				  RESULT = new Vector<Type>();
    				:}
    ;


ExprList ::=     Expr:_1
				 {:
				   Vector<STO> v = new Vector<STO>();
				   v.addElement(_1);
				   RESULT = v;
				 :}
    |               ExprList:_1 T_COMMA Expr:_2
    			 {:
    			   _1.addElement(_2);
    			   RESULT = _1;
    			 :}
    ;

Expr ::=         Designator:_1 T_ASSIGN Expr:_2
                 {:
                   RESULT = ((MyParser) parser).DoAssignExpr (_1, _2);
                 :}
    |            Expr0:_1
                 {:
			       RESULT = _1;
                 :}
    ;


Expr0 ::=        Expr0:_1 T_OR
				 {:

				    ((MyParser)parser).DoOrOpCheckFirst(_1);
				 :}					 
				 Expr1:_3
                 {:
					RESULT = ((MyParser) parser).DoBinaryExpr(_1, new OrOp(), _3);
                 :}
    |            Expr1:_1
                 {:
			       RESULT = _1;
                 :}
    ;


Expr1 ::=        Expr1:_1 T_AND
  				 {:
  				   ((MyParser)parser).DoAndOpCheckFirst(_1);
  				   
				 :} 
				 Expr2:_3
                 {:
                   RESULT = ((MyParser) parser).DoBinaryExpr(_1, new AndOp(), _3);
                 :}
    |            Expr2:_1
                 {:
			       RESULT = _1;
                 :}
    ;


Expr2 ::=        Expr2:_1 T_BAR Expr3:_3
                 {:
					RESULT = ((MyParser) parser).DoBinaryExpr(_1, new BwOrOp(), _3);
                 :}
    |            Expr3:_1
                 {:
                    RESULT = _1;
                 :}
    ;


Expr3 ::=        Expr3:_1 T_CARET Expr4:_3
                 {:
			       RESULT = ((MyParser) parser).DoBinaryExpr(_1, new BwXorOp(), _3);
                 :}
    |            Expr4:_1
                 {:
                   RESULT = _1;
                 :}
    ;


Expr4 ::=        Expr4:_1 T_AMPERSAND Expr5:_3
                 {:
			       RESULT = ((MyParser) parser).DoBinaryExpr(_1, new BwAndOp(), _3);
                 :}
    |            Expr5:_1
                 {:
                      RESULT = _1;
                 :}
    ;


Expr5 ::=           Expr5:_1 Equality:_2 Expr6:_3
                    {:
                    	if(_2.equals("=="))
                        	RESULT = ((MyParser) parser).DoBinaryExpr(_1, new EqualOp(), _3);
                        else
                        	RESULT = ((MyParser) parser).DoBinaryExpr(_1, new NotEqualOp(), _3);
                    :}
    |               Expr6:_1
                    {:
                        RESULT = _1;
                    :}
    ;


Expr6 ::=           Expr6:_1 Relation:_2 Expr7:_3
                    {:
                        //check #0
                    	if(_2.equals("<"))
                    	  RESULT = ((MyParser) parser).DoBinaryExpr(_1, new LessThanOp(), _3);
                        else if (_2.equals("<="))
                          RESULT = ((MyParser) parser).DoBinaryExpr(_1, new LessAndEqualThanOp(), _3);
                        else if(_2.equals(">"))
                          RESULT = ((MyParser) parser).DoBinaryExpr(_1, new GreaterThanOp(), _3);
                        else
                          RESULT = ((MyParser) parser).DoBinaryExpr(_1, new GreaterAndEqualThanOp(), _3);
                    :}
    |               Expr7:_1
                    {:
                        RESULT = _1;
                    :}
    ;


Expr7 ::=           Expr7:_1 AddOp:_2 Expr8:_3
                    {:
                        //Check #1
                        if(_2.equals("+"))
                          RESULT = ((MyParser) parser).DoBinaryExpr(_1, new AddOp(), _3);
                        else
                          RESULT = ((MyParser)parser).DoBinaryExpr(_1,new MinusOp(),_3);
                    :}
    |               Expr8:_1
                    {:
                        RESULT = _1;
                    :}
    ;


Expr8 ::=        Expr8:_1 MulOp:_2 Designator:_3
                    {:
                        //Check #1
                        if(_2.equals("*"))
                          RESULT = ((MyParser) parser).DoBinaryExpr(_1,new MulOp(), _3);
                        else if(_2.equals("/"))
                          RESULT = ((MyParser)parser).DoBinaryExpr(_1,new DivOp(),_3);
                        else
                          RESULT = ((MyParser)parser).DoBinaryExpr(_1,new ModOp(),_3); 
                    :}
    |               Designator:_1
                    {:
                        RESULT = _1;
                    :}
    ;


Equality ::=     T_EQU
				{:
					RESULT = "==";
				:}
    |               T_NEQ
    			{:
    				RESULT = "!=";
    			:}
    ;

//Phase 0
UnarySign ::=   T_PLUS
				{:
				  RESULT = "+";
				:}
	|			T_MINUS
				{:
				  RESULT = "-";
				:}
	;

Relation ::=     T_LT
					{:
						RESULT = "<";
					:}
    |               T_GT
                      {:
                        RESULT = ">";
                      :}
    |               T_LTE
                      {:
                        RESULT = "<=";
                      :}
    |               T_GTE
                      {:
                        RESULT = ">=";
                      :}
    ;


AddOp ::=       T_PLUS
                    {:
                        RESULT = "+";
                    :}
    |               T_MINUS
                    {:
                        RESULT = "-";
                    :}
    ;


MulOp ::=       T_STAR
                    {:
                        RESULT = "*";
                    :}
    |               T_SLASH
                    {:
                        RESULT = "/";
                    :}
    |               T_MOD
                    {:
                        RESULT = "%";
                    :}
    ;


IncDecOp ::=        T_PLUSPLUS
                    {:
                        RESULT = "++";
                    :}
    |               T_MINUSMINUS
                    {:
                        RESULT = "--";
                    :}
    ;


Designator ::= T_STAR Designator:_1
               {:
                 //Dereference operation
                 RESULT = ((MyParser) parser).doDereferenceCheck(_1);
               :}
           |   T_AMPERSAND Designator:_1
               {:
                 //Addressof operation
                 //Argument pass by reference
			     RESULT = ((MyParser) parser).doAddressOfCheck(_1);
               :}
    |   UnarySign:_2 Designator:_1
        {:
            if(_1.isError()){
              RESULT = _1;
            }
            else if(_2.equals("+")){
              if(_1.isConst()){
                ((ConstSTO)_1).setValue(+(((ConstSTO)_1).getValue()));
                RESULT = _1;
              }
              else{
                ExprSTO ret = new ExprSTO(_1.getName(), _1.getType());
                ret.setBase(_1.getBase());
                ret.setOffset(_1.getOffset());
                RESULT = ret;
              }  
			}
			else{
			  if(_1.isConst()){
			    ConstSTO ret = new ConstSTO(_1.getName());
			    ret.setType(_1.getType());
			    ret.setValue(-(((ConstSTO)_1).getValue()));
			    RESULT = ret;
			  }
			  /*else if(_1.isVar()){
			    ((VarSTO)_1).setNegative(true);
			    ExprSTO ret = new ExprSTO("-" + _1.getName(), _1.getType());
			    RESULT = ret;
			  }*/
			  else{
			    ExprSTO ret = new ExprSTO("-" + _1.getName(), _1.getType());
			    RESULT = ((MyParser) parser).doMinusUnary(_1);
			  }
			}
        :}
    |   T_NOT Designator:_1
        {:
            RESULT = ((MyParser) parser).DoUnaryExpr(_1, new NotOp());
        :}
    |   T_SIZEOF T_LPAREN Designator:_2 T_RPAREN
        {:
		  RESULT = ((MyParser) parser).DoSizeOfDes(_2);
        :}
    |   T_SIZEOF T_LPAREN Type:_2 T_RPAREN
        {:
	      RESULT = ((MyParser) parser).DoSizeOfType(_2);
        :}
    |   T_LPAREN Type:_1 T_RPAREN Designator:_2
        {:
        //Check 20 Type cast
          RESULT = ((MyParser) parser).DoCastCheck(_1,_2);
        :}
    |   IncDecOp:_1 Designator:_2
        {:  
            if(_1.equals("++"))
			  RESULT = ((MyParser) parser).DoUnaryExpr(_2, new IncOp("preIncOp"));
			else
			  RESULT = ((MyParser) parser).DoUnaryExpr(_2, new DecOp("preDecOp"));
        :}
    |               Designator2:_1 
                    {:
                        RESULT = _1;
                    :}
    ;


Designator2 ::=     Designator2:_1 T_DOT T_ID:_3
                    {:
                        RESULT = ((MyParser) parser).DoDesignator2_Dot (_1, _3);
                    :}
    |               Designator2:_1 T_LBRACKET Expr:_2 T_RBRACKET
                    {:
                        //Check#11
                        RESULT = ((MyParser) parser).DoDesignator2_Array (_1, _2);
                    :}
    |               Designator2:_1 T_ARROW T_ID:_2
                    {:
                       //Struct dereference
                        RESULT = ((MyParser) parser).DoArrowOp(_1,_2);
                    :}
    |               Designator2:_1 IncDecOp:_2
                    {:
                    	if(_2.equals("++"))
                          RESULT = ((MyParser) parser).DoUnaryExpr(_1, new IncOp("postIncOp"));
                        else
                          RESULT = ((MyParser) parser).DoUnaryExpr(_1, new DecOp("postDecOp"));
                    :}
    |               Designator2:_1 T_LPAREN OptExprList:_2 T_RPAREN
                    {:
                        //Do function call check #5
                        //OptExprList containing all the parameters
                        RESULT = ((MyParser) parser).DoFuncCall (_1, _2);
                    :}
    |               Designator3:_1
                    {:
                        RESULT = _1;
                    :}
    ;


Designator3 ::=     T_LPAREN Expr:_2 T_RPAREN
                    {:
                        RESULT = _2;
                    :}
    |               T_INT_LITERAL:_1
                    {:
                        ConstSTO s = new ConstSTO(_1,new IntType("int",4));
                        try{
	                        int value = Integer.decode(_1);
	                        s.setValue(value);
	                    }
	                    catch(NumberFormatException e){
	                      //TODO
	                    }
                        RESULT = s;
                        
                    :}
    |               T_FLOAT_LITERAL:_1
                    {:
                        ConstSTO s = new ConstSTO (_1, new FloatType("float", 4));
                        s.setValue(Double.parseDouble(_1));
                        RESULT = s;
                        
                    :}
    |               T_CHAR_LITERAL:_1
                    {:
                        RESULT = new ConstSTO (_1);
                    :}
    |               T_STR_LITERAL:_1
                    {:
                        RESULT = new ConstSTO (_1);
                    :}
    |               T_TRUE
                    {:
                        ConstSTO s = new ConstSTO ("true", new BoolType("bool",4));
                        s.setValue(1.0);
                        RESULT = s;
                    :}
    |               T_FALSE
                    {:
                        ConstSTO s = new ConstSTO ("false", new BoolType("bool",4));
                        s.setValue(0.0);
                        RESULT = s;
                    :}
    |               T_NULLPTR
                    {:
                      	ConstSTO s = new ConstSTO ("nullptr", new NullPointerType("nullptr", 4));
                      	s.setValue(0.0);
                      	RESULT = s;
                    :}
    |               T_COLONCOLON T_ID:_1
    				//Check #0 global variable check
                    {:
                        RESULT = ((MyParser) parser).DoDesignator3_Global_ID (_1);
                    :}
    |               T_ID:_1
                    {:
                        RESULT = ((MyParser) parser).DoDesignator3_ID (_1);
                    :}
    ;


QualIdent ::=       T_ID_U:_1
                    {:
                       //Return a typedefSTO
                       RESULT = ((MyParser) parser).DoQualIdent (_1);
                    :}
    ;
